apply plugin: 'com.android.application'
apply from: rootProject.file('andResConfig.gradle')
android {
    compileSdkVersion rootProject.ext.versions.compileSdkVersion
    buildToolsVersion rootProject.ext.versions.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.versions.applicationId
        minSdkVersion rootProject.ext.versions.minSdkVersion
        targetSdkVersion rootProject.ext.versions.targetSdkVersion
        versionCode rootProject.ext.versions.versionCode
        versionName rootProject.ext.versions.versionName
        testInstrumentationRunner "android.support.xxx_test.runner.AndroidJUnitRunner"
        //MultiDex分包方法
        multiDexEnabled true

        //Arouter路由配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
                includeCompileClasspath = true
            }
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: rootProject.ext.versions["applicationId"],
                JPUSH_APPKEY : rootProject.ext.keys["jPushKey"],
                JPUSH_CHANNEL: rootProject.ext.keys["jPushChannel"],
                qqappid: rootProject.ext.versions["applicationId"],
        ]
    }//自定义输出apk名字
    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                outputFileName = new File("App/app_v" + defaultConfig.versionName + "_" +
                        new Date().format("yyyy-MM-dd") + "_" +
                        buildType.name + ".apk")
            }
    }
    signingConfigs {
        config {
            keyAlias rootProject.ext.sign.keyAlias
            keyPassword rootProject.ext.sign.keyPassword
            storeFile rootProject.ext.sign.storeFile
            storePassword rootProject.ext.sign.storePassword
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled true //混淆编译
            shrinkResources true //移除无用资源
            zipAlignEnabled true //zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:27.1.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor rootProject.ext.dependencies["butterknife_compiler"]

    //公用依赖包
    implementation project(':common')
    if (!Boolean.valueOf(rootProject.ext.isModule)) {
        //main模块
        //TODO
        implementation project(':basemoudle')

    }
}